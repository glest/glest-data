##################################################################################
# Game data
CMAKE_MINIMUM_REQUIRED( VERSION 2.8.2 )
PROJECT( ZetaGlest )

OPTION(WANT_INSTALL_DATA "Install ZetaGlest's data" ON)

# Requires an install prefix for the items below to work
IF(NOT CMAKE_INSTALL_PREFIX STREQUAL "" AND WANT_INSTALL_DATA)
	MESSAGE(STATUS "**Data package INSTALL triggered.")

	SET(COMMON_INFO_ABOUT_PATH "(if the path is relative then is appended to the CMAKE_INSTALL_PREFIX)")
	IF(NOT ZETAGLEST_DATA_INSTALL_PATH)
		SET(ZETAGLEST_DATA_INSTALL_PATH "share/zetaglest/" CACHE STRING "The installation path for data files ${COMMON_INFO_ABOUT_PATH}")
	ENDIF()
	IF(NOT ZETAGLEST_APPDATA_INSTALL_PATH AND NOT WANT_SINGLE_INSTALL_DIRECTORY)
		SET(ZETAGLEST_APPDATA_INSTALL_PATH "share/metainfo/" CACHE STRING "The installation path for appdata files ${COMMON_INFO_ABOUT_PATH}")
	ENDIF()
	IF(NOT ZETAGLEST_DESKTOP_INSTALL_PATH)
		SET(ZETAGLEST_DESKTOP_INSTALL_PATH "share/applications/" CACHE STRING "The installation path for desktop files ${COMMON_INFO_ABOUT_PATH}")
	ENDIF()
	IF(NOT ZETAGLEST_ICON_INSTALL_PATH)
		SET(ZETAGLEST_ICON_INSTALL_PATH "share/pixmaps/" CACHE STRING "The installation path for icon files ${COMMON_INFO_ABOUT_PATH}")
	ENDIF()
	IF(NOT ZETAGLEST_MENU_INSTALL_PATH AND NOT WANT_SINGLE_INSTALL_DIRECTORY)
		#SET(ZETAGLEST_MENU_INSTALL_PATH "share/menu/" CACHE STRING "The installation path for menu file ${COMMON_INFO_ABOUT_PATH}")
	ENDIF()
	IF(NOT ZETAGLEST_BIN_INSTALL_PATH AND NOT "${ZETAGLEST_MENU_INSTALL_PATH}" STREQUAL "")
		SET(ZETAGLEST_BIN_INSTALL_PATH "bin/" CACHE STRING "The installation path for binaries ${COMMON_INFO_ABOUT_PATH}")
	ENDIF()
	FOREACH(ZG_PATH BIN ICON)
		IF(NOT "${ZETAGLEST_${ZG_PATH}_INSTALL_PATH}" STREQUAL "")
			IF(IS_ABSOLUTE "${ZETAGLEST_${ZG_PATH}_INSTALL_PATH}")
				SET(ZETAGLEST_FULL_${ZG_PATH}_INSTALL_PATH "${ZETAGLEST_${ZG_PATH}_INSTALL_PATH}/")
			ELSE()
				SET(ZETAGLEST_FULL_${ZG_PATH}_INSTALL_PATH "${CMAKE_INSTALL_PREFIX}/${ZETAGLEST_${ZG_PATH}_INSTALL_PATH}/")
			ENDIF()
			STRING(REGEX REPLACE "//+" "/" ZETAGLEST_FULL_${ZG_PATH}_INSTALL_PATH "${ZETAGLEST_FULL_${ZG_PATH}_INSTALL_PATH}")
		ENDIF()
	ENDFOREACH()

	CONFIGURE_FILE("${PROJECT_SOURCE_DIR}/others/menu/zetaglest.menu.in"
		"${PROJECT_BINARY_DIR}/others/menu/zetaglest.menu.in" COPYONLY)
	IF(BUILD_ZETAGLEST_MAP_EDITOR OR NOT DEFINED BUILD_ZETAGLEST_MAP_EDITOR)
		FILE(READ "${PROJECT_SOURCE_DIR}/others/menu/zetaglest_editor.menu.in" EDITOR_MENU)
		FILE(APPEND "${PROJECT_BINARY_DIR}/others/menu/zetaglest.menu.in" "${EDITOR_MENU}")
	ENDIF()
	CONFIGURE_FILE("${PROJECT_BINARY_DIR}/others/menu/zetaglest.menu.in"
		"${PROJECT_BINARY_DIR}/others/menu/zetaglest")

	# Installation of the data
	INSTALL(DIRECTORY "${PROJECT_SOURCE_DIR}/data"
			"${PROJECT_SOURCE_DIR}/docs"
			"${PROJECT_SOURCE_DIR}/maps"
			"${PROJECT_SOURCE_DIR}/scenarios"
			"${PROJECT_SOURCE_DIR}/techs"
			"${PROJECT_SOURCE_DIR}/tilesets"
			"${PROJECT_SOURCE_DIR}/tutorials"
			DESTINATION ${ZETAGLEST_DATA_INSTALL_PATH}
			OPTIONAL REGEX "/.git" EXCLUDE)

	IF(UNIX AND NOT "${ZETAGLEST_APPDATA_INSTALL_PATH}" STREQUAL "")
		INSTALL(FILES "${PROJECT_SOURCE_DIR}/others/appdata/zetaglest.appdata.xml"
			DESTINATION ${ZETAGLEST_APPDATA_INSTALL_PATH})

		IF(BUILD_ZETAGLEST_MAP_EDITOR OR NOT DEFINED BUILD_ZETAGLEST_MAP_EDITOR)
			INSTALL(FILES "${PROJECT_SOURCE_DIR}/others/appdata/zetaglest_editor.appdata.xml"
				DESTINATION ${ZETAGLEST_APPDATA_INSTALL_PATH})
		ENDIF()
	ENDIF()
	IF(UNIX AND NOT "${ZETAGLEST_MENU_INSTALL_PATH}" STREQUAL "")
		INSTALL(FILES
			"${PROJECT_BINARY_DIR}/others/menu/zetaglest"
			DESTINATION ${ZETAGLEST_MENU_INSTALL_PATH})
	ENDIF()

	IF(UNIX AND NOT APPLE)
		INSTALL(FILES
			"${PROJECT_SOURCE_DIR}/others/icons/zetaglest.png"
			"${PROJECT_SOURCE_DIR}/others/icons/zetaglest.xpm"
			DESTINATION ${ZETAGLEST_ICON_INSTALL_PATH})

		INSTALL(FILES
			"${PROJECT_SOURCE_DIR}/others/desktop/zetaglest.desktop"
			DESTINATION ${ZETAGLEST_DESKTOP_INSTALL_PATH})
		IF(BUILD_ZETAGLEST_MAP_EDITOR OR NOT DEFINED BUILD_ZETAGLEST_MAP_EDITOR)
			INSTALL(FILES
				"${PROJECT_SOURCE_DIR}/others/desktop/zetaglest_editor.desktop"
				DESTINATION ${ZETAGLEST_DESKTOP_INSTALL_PATH})
		ENDIF()
		IF(BUILD_ZETAGLEST_MODEL_VIEWER OR NOT DEFINED BUILD_ZETAGLEST_MODEL_VIEWER)
			INSTALL(FILES
				"${PROJECT_SOURCE_DIR}/others/desktop/zetaglest_g3dviewer.desktop"
				DESTINATION ${ZETAGLEST_DESKTOP_INSTALL_PATH})
		ENDIF()
	ENDIF()
ENDIF()
